apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def compileDependencies = rootProject.ext.dependencies
def testDependencies = rootProject.ext.testDependencies

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "br.com.neorose.uberfy"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "UBER_CLIENT_ID", "\"${getProps('uber.clientid')}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    dexOptions {
        javaMaxHeapSize "2048M"
    }
}

kapt {
    generateStubs = true
}

def getProps(String propName) {
    def propsFile = rootProject.file('local.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[propName]
    } else {
        return "";
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile compileDependencies.kotlin_stdlib

    compile compileDependencies.support_v7
    compile compileDependencies.recyclerView
    compile compileDependencies.cardview
    compile compileDependencies.support_design

    compile compileDependencies.dagger2
    kapt compileDependencies.dagger2_compiler
    provided compileDependencies.jsr250

    compile compileDependencies.retrofit
    compile compileDependencies.retrofit_converter_gson
    compile compileDependencies.retrofit_rx_java
    compile compileDependencies.okhttp
    compile compileDependencies.okhttp3_logging_interceptor

    compile compileDependencies.rxkotlin
    compile compileDependencies.rxandroid

    compile compileDependencies.uber_sdk
}
